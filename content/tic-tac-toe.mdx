---
title: Tic-Tac-Toe in Assembly Language
publishedAt: 2020-12-13
summary: One of my first programming projects. Tic-Tac-Toe programmed in x86-64 assembly language. Over-documented to show a line-by-line understanding.
tags: Assembly Language
---

<SourceCodeButton sourceUrl={'https://github.com/jjblek/tic-tac-toe'}/>

## Table of Contents
1. [Introduction](#introduction)
2. [ASM Program: Tic-Tac-Toe](#tic-tac-toe)
    - [Data Section](#data-section)
    - [BSS Section](#bss-section)
    - [Code Section](#code-section)
        - [Macros](#macros)
        - [Loops](#loops)
        - [Macro vs. Function](#macro-vs-function)
    - [Program Logic](#program-logic)
        - [Game Start](#game-start)
        - [Round Start](#round-start)
        - [Input Start](#input-start)
        - [Placement Start](#placement-start)
        - [Print New Board](#print-new-board)
        - [Check win Conditions](#check-win)
        - [Round End](#round-end)
        - [Game End](#game-end)
3. [Conclusion](#conclusion)

---

## Introduction

Assembly language, often referred to as Assembly and abbreviated as ASM, is any low-level programming language with a very strong correspondence between the instructions in the language and the architecture's machine code instructions.

A program written in assembly language consists of instructions, statements, comments and data. Assembly language instructions usually consist of an opcode mnemonic followed by an operand, which might be a list of data, arguments or parameters. They are then translated by an assembler into machine language instructions, loaded into memory and executed.

For example, the instruction below tells an x86/IA-32 processor to move an 8-bit value into a register. The binary code for this instruction is 10110 followed by a 3-bit identifier for which register to use. The identifier for the AL register is 000, so the following machine code loads the AL register with the data 01100001.

```asm
10110000 01100001
```

This binary computer code can be made more human-readable by expressing it in hexadecimal as follows.

```asm
B0 61
```

Here, B0 means 'Move a copy of the following value into AL, and 61 is a hexadecimal representation of the value 01100001, which is 97 in decimal.

Assembly language for the 8086 family provides the mnemonic MOV (an abbreviation of move) for instructions such as this, so the machine code above can be written as follows in assembly language.

```asm
MOV AL, 61       ; Load AL with 97 (61 in hex)
```

Assembly language is considered a low-level language because it provides little to no abstraction from a computer's architecture. 

Moving a value into a register is further abstracted in higher-level languages. For example, the following C++ code initializes an integer value to 97.

```cpp
int value = 97; // Initialize integer value to 97
```

If we want to store a value in assembly language it is possible to allocate space for initialized and uninitialized data.

```asm
; [variable-name]    define-directive    initial-value 
initialized         db                  61  ; Define 1 byte
```

The above assembly code defines a variable initialized to the hex value of 97 with the `db` (define byte) directive. Variables defined this way would correspond to global or static variables in a higher-level language.

However, if we assume the above C++ code is not global, then when compiled into assembly language it would likely result in a `MOV` instruction like the one given above.

---

<a name="tic-tac-toe"></a>
## ASM Program: Tic-Tac-Toe

<a name="data-section"></a>
### Data Section

The first section of the program is the **data section**. It uses define directives such as `db` to not only allocate space for variables, but to make the rest of the program more human-readable/modifiable.

```asm
; Data Section
; ############
section     .data

; ###############################################
; Variable Definitions
; ###############################################
; constants for system calls
SYS_read     equ 0x0
SYS_stdin    equ 0x0
SYS_write    equ 0x1
SYS_stdout   equ 0x1
SYS_exit     equ 0x3c
exit_success equ 0x0
endl         db 0x0a
space        db " "

; tic-tac-toe game pieces and board
player1     db "X"
player2     db "O"
tttIndex    db "# 123"
tttArray    db "---------"
tttRow      dq 0x3
tttCol      dq 0x3
```

Above, we initialize variables for system calls and redundant data.
Below, we initialize variables for various prompts and error messages.
Similar to globals in higher-level languages, defining our symbols and prompts in the data section makes them easy to modify later on.

```asm
; start prompt
title        db "     TIC-TAC-TOE              "
exitPrompt   db "Enter 0 (zero) to EXIT         "
instructions db "22 means row 2, column 2      "

; round prompts
p1Prompt    db  "Player 1's turn! Place an X!"
p2Prompt    db  "Player 2's turn! Place an O!"
promptSize  equ $-p2Prompt

; input prompt
inputPrompt db  "Enter row/col number: "
userInput   db  "                      "
inputSize   equ $-userInput

; error messages
inputError  db  "Invalid Input, try again..    "
placeError  db  "Invalid Placement, try again.."
errorSize   equ $-placeError

; exit prompts
p1WinPrompt   db  "Player 1 wins!!!"
p2WinPrompt   db  "Player 2 wins!!!"
drawPrompt    db  "Draw!!!         "
endPromptSize equ $-drawPrompt
```

<Callout emoji="ðŸ’¡">
**Note**: `db` can define more than one byte. The assembler allocates contiguous memory.
</Callout>

<a name="bss-section"></a>
### BSS Section

The next section, called the **bss section** contains declarations of variables that have not yet been assigned a value. In this program, a user inputs a row and column number to place a symbol on the tic-tac-toe board. So, we will reserve space for the user's row and column input using the `resb` directive.

```asm
; BSS Section
; ############ 
section     .bss

row resb 1 ; reserve a byte for row input

col resb 1 ; reserve a byte for column input
```

<a name="code-section"></a>
### Code Section

Lastly, the **code section** is the portion of the program that contains executable instructions like `MOV`.

<a name="macros"></a>
#### Macros

At the beginning of this section we define a re-usable unit of code called a **Macro**. Macros are used to contain tasks or redundant code. They help to simplify logic and management of the source code.

The first macro, `print` is used to print a specified prompt. It has 2 parameters represented in hexadecimal as `0x2`. The first parameter is the address of a prompt and the second is the size of that prompt.

```asm
; Code Section
; ############
section     .text

; ###############################################
; ### Begin Program 
; ###############################################
global _start

_start:
; ###############################################
; ### Macro: output prompt
; ###############################################
%macro print 0x2

mov rax, SYS_write  ; call code = 0x1
mov rdi, SYS_stdout ; output location
mov rsi, %1         ; address of output
mov rdx, %2         ; output size
syscall             ; invoke call to system service

mov rax, SYS_write  ; call code = 0x1
mov rdi, SYS_stdout ; output location = 0x1
mov rsi, endl       ; endl = 0x0a
mov rdx, SYS_write  ; size of output = call code
syscall             ; invoke call to system service

%endmacro
```

The second macro, `printRow` is used to print a row of the tic-tac-toe board given an index. It is used inside another macro, `printBoard`.

```asm
; ###############################################
; ### Macro: print a row
; ###############################################
%macro printRow 0x1

push rcx                ; save counter

mov rax, qword[tttRow]  ; size of the row
mov rbx, %1             ; move the row number to print into rbx
dec rbx                 ; decrement to 0 offset
mul rbx                 ; multiply rax by rbx for the correct point in the array

lea rsi, [tttArray+rax] ; set the address of rsi to the correct point in tttArray
mov rdx, qword[tttRow]  ; size of the row

print rsi, rdx          ; pass address and size to print
pop rcx                 ; restore counter

%endmacro
```

<a name="loops"></a>
#### Loops

The `printBoard` macro shows how loops in assembly language are created, using labels and jump instructions to print each row of the tic-tac-toe board.

The **label** `%%printRowStart` is the beginning of the loop. The **jump** instruction `jbe %%printRowStart` is the end of the loop.

A jump instruction jumps to a specified label. `JBE` stands for jump-below-equal or jump if less-than or equal. It depends on the result of the compare instruction `CMP RCX, [tttRow]` one line above. The `RCX` register contains the number of rows printed and is compared to the max tic-tac-toe row size of 3. Therefore, `JBE` will jump to the starting label while `RCX` is less than 3.

```asm
; ###############################################
; ### Macro: print the tic-tac-toe board
; ###############################################
%macro printBoard 0x0

print tttIndex, 0x5 ; print column index above the board

mov rcx, 0x1        ; start counter 1
%%printRowStart:
push rcx            ; save counter

; print row index
mov rax, SYS_write              ; call code
mov rdi, SYS_stdout             ; output location
lea rsi, byte [tttIndex+rcx+1]  ; address of output
mov rdx, SYS_write              ; size of output = call code
syscall                         ; invoke call to system service

; print a space
mov rax, SYS_write  ; call code
mov rdi, SYS_stdout ; output location
mov rsi, space      ; address of output
mov rdx, SYS_write  ; size of output = call code
syscall             ; invoke call to system service

; print a row
pop rcx             ; restore counter
printRow rcx        ; pass counter to the 'printRow' macro
inc rcx             ; increment the counter after printing a row
cmp rcx, [tttRow]   ; compare the counter to row size
jbe %%printRowStart ; continue the loop if counter is below or equal to 3

; print an endline
mov rax, SYS_write  ; call code
mov rdi, SYS_stdout ; output location
mov rsi, endl       ; address of output
mov rdx, SYS_write  ; size of output = call code
syscall             ; invoke call to system service

%endmacro
```

<Callout emoji="ðŸ’¡">
**Note**: The `CMP` instruction can be thought of as an `if` statement. A normal `JMP` instruction is unspecified, meaning it does not depend on the result of a `CMP` instruction.
</Callout>

The final macro, `checkPlacement` is used to check if an X or O is already placed on a user's row and column input. 

```asm
; ###############################################
; ### Macro: check if placement is valid
; ###############################################
%macro checkPlacement 0x0

mov bl, byte [tttArray + rax] ; store placement position
cmp bl, [player1]             ; if "X" is already in this placement position
je invalidPlacement           ; then selected placement is invalid
; else continue
cmp bl, [player2]   ; if "O" is already in this placement position
je invalidPlacement ; then selected placement is invalid
jmp validPlacement  ; else valid placement

%endmacro
```

<a name="macro-vs-function"></a>
#### Macro vs. Function/Procedure

Note that we jump to a label outside of the macro. While connections can be made between a macro and a function, it is important to note the distinction between the two. When a macro is called, it simply executes the group of instructions contained inside.

Functions in higher-level languages correspond to a procedure in assembly language, not a macro. However, like a function/procedure, a macro improves readability and reduces repetitive code. The advantage is that it does not need a stack or require extra time to call and return.

<Callout emoji="ðŸ’¡">
**Note**: Macros can be defined in C++ with the `#define` directive. However, functions are often preferred.
</Callout>

<a name="program-logic"></a>
### Program Logic

Following the macro definitions, the program's core logic begins, where the actual instructions are implemented and executed.

<a name="game-start"></a>
#### Game Start

The first section prints a title and game instructions. It then prints an empty tic-tac-toe board using the `printBoard` macro defined above.

```asm
; ###############################################
; ### Game start
; ###############################################
mov rax, SYS_write  ; call code = 0x1
mov rdi, SYS_stdout ; output location = 0x1
mov rsi, endl       ; endl = 0x0a
mov rdx, SYS_write  ; size of output = call code
syscall             ; invoke call to system service

; start prompts
print title, promptSize         ; "TIC-TAC-TOE"
print exitPrompt, promptSize    ; "Enter 0 (zero) to EXIT"
print instructions, promptSize  ; "22 means row 2, column 2"

mov rax, SYS_write  ; call code = 0x1
mov rdi, SYS_stdout ; output location = 0x1
mov rsi, endl       ; endl = 0x0a
mov rdx, SYS_write  ; size of output = call code
syscall             ; invoke call to system service

printBoard          ; print default board
```

<a name="round-start"></a>
#### Round Start

The next section represents the start of a tic-tac-toe round. Each round begins by prompting a player to make a placement. The `RCX` register is now used to count the number of rounds and the `roundStart` label represents the start of a round/program loop. The program essentially loops through each round until a win condition is met.

```asm
; ### Round start
; ###############################################
xor rcx, rcx  ; round counter

roundStart:
inc rcx       ; increment round counter
push rcx      ; save round counter

; player 1 (X) is odd
; player 2 (O) is even
test rcx, 0x1 ; test round counter
jz p2Turn     ; jump to "p2Turn" if even/lowest bit clear

; else lowest bit set; odd
mov r8b, [player1]          ; r8b holds "X"
print p1Prompt, promptSize  ; output: "Player 1's turn! Place an X!"
jmp continue                ; continue to input

p2Turn:
mov r8b, [player2]          ; r8b holds "O"
print p2Prompt, promptSize  ; output: "Player 2's turn! Place an O!"
jmp continue                ; continue to input

invalidPlacement:             ; jumped from the checkPlacement macro
 print placeError, errorSize  ; output: "Invalid Placement, try again.."

continue:
```

<a name="input-start"></a>
#### Input Start

This section demonstrates the input handling mechanism in assembly language, where user input for board placement is solicited, captured, and rigorously validated. Invalid inputs trigger error messaging and prompt a re-entry sequence.

```asm
; ### Input start
; ###############################################
inputStart:

; output 'inputPrompt' to console
mov rax, SYS_write    ; call code = 0x1
mov rdi, SYS_stdout   ; output location
mov rsi , inputPrompt ; address of output
mov rdx, inputSize    ; output size
syscall               ; invoke call to system service

; get input from console
mov rax, SYS_read   ; call code = 0x0
mov rdi, SYS_stdin  ; input location
mov rsi, userInput  ; address to store input
mov rdx, inputSize  ; size of input
syscall             ; invoke call to system service

; the first byte in rsi or "userInput" is the row input
mov al, byte [rsi]  ; store row input in the accumulator register
sub al, 0x30        ; subtract 48 to convert ASCII to decimal
jz exit             ; if row input is zero; exit program
cmp al, 0x3         ; if row input is greater than 3
jg invalidInput     ; jump to "invalidInput"
; else row input is valid, continue check for column input

; the second byte in rsi or "userInput" is the column input
mov bl, byte [rsi+1]  ; store column input in the base register
sub bl, 0x30          ; subtract 48 to convert ASCII to decimal
jz exit               ; if column input is zero; exit program
cmp bl, 0x3           ; if column input is greater than 3
jg invalidInput       ; jump to "invalidInput"
cmp bl, 0x0a          ; if the second byte is an endl
je invalidInput       ; jump to "invalidInput"
; else column input is valid, continue check for endl

; if input is valid, the third byte will be 0x0a (endl)
cmp byte [rsi+2], 0x0a  ; if the 3rd byte is not an endl
jne invalidInput        ; jump to "invalidInput"
; else valid input
mov byte [row], al      ; set row
mov byte [col], bl      ; set column
jmp validInput          ; jump to "validInput"

invalidInput:
 print inputError, errorSize  ; output: "Invalid input, try again.."
jmp inputStart                ; restart input, jump to "inputStart"

validInput: ; input is valid, placement can start
```

<a name="placement-start"></a>
#### Placement Start

Beyond validating the input format, it's crucial to ensure the selected board location is an available placement. The `checkPlacement` macro provides this validation mechanism.

```asm
; ### Placement start
; ###############################################

mul bl              ; row*col gives the correct placement for row 1 or column 3
cmp byte [row], 0x1 ; if row = 1
je r1ORc3           ; or
cmp byte [col], 0x3 ; if col = 3 
je r1ORc3           ; jump to placement for row 1 or column 3 
; else
cmp al, 0x4         ; if row*col = 4; row 2, column 2
je r2c2             ; else continue
cmp al, 0x3         ; if row*col = 3; row 3, column 1
je r3c1             ; else continue

; row 2, column 1 or row 3, column 2 

inc rax         ; increment the product for the correct placement in tttArray
 checkPlacement

r2c2:           ; row 2, column 2
 checkPlacement ; product = placement

r3c1:           ; row 3, column 1
add rax, 0x3    ; add 3 to the product for the correct placement in tttArray
 checkPlacement 

r1ORc3:         ; row 1 OR column 3
dec rax         ; decrement the product for the correct placement in tttArray
 checkPlacement

validPlacement: ; set valid placement
mov byte [tttArray + rax], r8b
```

<a name="print-new-board"></a>
#### Print New Board

Making use of the `printBoard` macro, if a placement is valid the program will print a new board showing the new placement.

```asm
; ### Print new board
; ###############################################

printBoard    ; print new board

pop rcx       ; restore counter
cmp rcx, 0x5  ; if counter is less than 5 it's impossible to win
jl nextRound  ; a few operations to skip hundreds
push rcx      ; save counter
```

After printing a new board the program checks the round counter. A player must place three symbols to win the game, so it is impossible to win until at least round five. Therefore we can skip checking win conditions until round five.

<a name="check-win"></a>
#### Check Win Conditions

The following section checks if a win condition has been met. If a win condition has been met the program will break from the loop. To break from the loop we can simply jump to a label outside of the loop. In this case we jump to the `win` label at the end of the program.

```asm
; ### Check win conditions
; ###############################################

; starting at the tail of the current row
mov al, byte [row]  ; accumulator = row
mov bl, 0x3         ; base = 3
mul bl              ; accumulator*base = row tail + 1
mov rcx, 0x3        ; set counter to 3
checkRows:          ; tttArray+rax-1 = row tail
cmp r8b, byte [tttArray+rax-1]  ; if current placement != row position
jne exitRows        ; exit row win check
dec rcx             ; else decrement counter
jz win              ; if counter = 0; win condition found, exit loop
dec al              ; decrement to traverse row tail to head
jmp checkRows       ; restart: check next row position
exitRows:

; starting at the head of the column from the current placement
mov al, byte [col]  ; accumulator = column
mov rcx, 0x3        ; set counter to 3
checkColumns:
cmp r8b, byte [tttArray+rax-1]  ; if current placement != column position
jne exitColumns     ; exit column win check
dec rcx             ; else decrement counter
jz win              ; if counter = 0; win condition found, exit loop
add al, 0x3         ; add 3 to traverse from column head to tail
jmp checkColumns    ; restart: check next column position
exitColumns:

; Diagonal 1: [0, 4, 8] 
mov rcx, 0xC        ; counter = 12
checkDiag1:
cmp rcx, 0x0        ; if counter = 0
je win              ; win condition found, exit loop
sub rcx, 0x4        ; subtract 4 to traverse from tail to head 
cmp r8b, byte [tttArray+rcx]  ; if current placement = diagonal position
je checkDiag1       ; restart: check next diagonal position

; Diagonal 2: [2, 4, 6]
mov rcx, 0x8      ; counter = 8
checkDiag2:
cmp rcx, 0x2      ; if counter = 2
je win            ; win condition found, exit loop
sub rcx, 0x2      ; subtract 2 to traverse from tail to head
cmp r8b, byte [tttArray+rcx]  ; if current placement = diagonal position
je checkDiag2     ; restart: check next diagonal positon

; else no win conditions found
```

<a name="round-end"></a>
#### Round End

If no win conditions are found and the board is full (ninth round), then the current game has resulted in a draw. The program will break from the loop by jumping to the `draw` label at the end of the program. Else the round has ended and no win/draw conditions were found, so start a new round by jumping to the `roundStart` label.

```asm
; ### Round end
; ###############################################

pop rcx         ; restore round counter
cmp rcx, 0x9    ; if round counter = 9, the board is full
je draw         ; exit; the current game is a draw
nextRound:      ; else
jmp roundStart  ; start next round
```

<a name="game-end"></a>
#### Game End

The game will end when a win or draw condition is met. The program outputs a message indicating the result and exits.

```asm
; ### Output win or draw
; ###############################################
win:

pop rcx       ; restore round counter
test rcx, 0x1 ; test round counter
jz p2win      ; if even, player 2 wins
; else player 1 wins

 print p1WinPrompt, endPromptSize ; output: "Player 1 wins!!!"
jmp exit

p2win:
 print p2WinPrompt, endPromptSize ; output: "Player 2 wins!!!"
jmp exit

draw:
 print drawPrompt, endPromptSize  ; output: "Draw!!!"

exit:
; ###############################################
; ### Game exit with exit code 0x0
; ###############################################
    mov rax, SYS_exit     ; call code = 0x3c
    mov rdi, exit_success ; output location = 0x0
    syscall
```
---

<a name="conclusion"></a>
## Conclusion: The Beauty of Low-Level Programming

The journey through this Tic-Tac-Toe implementation in assembly language reveals the intricate world of low-level programming. While higher-level languages abstract away the complexities of computer architecture, assembly language provides a raw, unfiltered view of how computers truly execute instructions.

This implementation demonstrates several key aspects of assembly programming:

- The direct manipulation of system resources through syscalls
- Precise control over register usage and memory allocation
- Creative use of macros to simplify repetitive code
- Implementing complex logic through low-level instructions and jumps

Beyond its technical complexity, this example showcases the fundamental principles of programming: breaking down a problem into smaller, manageable components. From managing game state to checking win conditions, each section of the code represents a carefully crafted solution that communicates directly with the computer's hardware.

For aspiring programmers and computer science enthusiasts, diving into assembly language provides a deeper understanding of how software truly interacts with hardware. It's a reminder that behind every high-level abstraction lies a intricate dance of bytes, registers, and precise instructions.

While we may not write entire applications in assembly today, understanding its principles can make us more thoughtful, efficient programmers across all languages and platforms.
